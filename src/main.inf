Constant TEST_VERSION;

Constant Story "Heist";
Constant Headline
    "^A sneaky thief adventure^
     By Ness Morris (2018)^^";

Include "Parser";

Fake_Action AttemptToDamage;

Object LibraryMessages
  with before [;
         !Examine:
       ],
       after [;
       ];

Include ">parser_errors.h";

Include "VerbLib";

Include ">devices/disguise_amulet";
Include ">devices/destruction_disk";
Include ">devices/portable_hole";
Include ">devices/universal_key";

Include ">clothes.h";

Class ObjectiveDevice;

Class SelfBodyPart;

Class Room;

Class Scenic
  has scenery;

Room Test_Chamber "Test Chamber"
  has light,
  with  name 'chamber' 'room',
        initial
          "^^
           ~Here I am in the test chamber. Plain as ever, with its
            featureless white walls and floor, flourescent lighting,
            and no apparent way out. I never understand why I have to
            be incapacitated, wake up here, work, and then be knocked
            out again to wake up at home. I mean, I don't have to commute
            (or at least be awake for it), but it is all a little bit...
            disconcerting. Especially the fact that my job has me working
            in a room without window or doors. But here I am, getting all
            personal when I know it's against the rules. But I am supposed
            to report my feelings, and the lack of exits leaves me feeling
            definitively disconcerted.~",
        description [;
          "~It's a featureless white room, lit with flourescent panels.~",
        "^^~There's a stainless-steel table with its legs are bolted
            into the floor in the center of the room. It would look at
            home in a hospital.~";
        ],
        s_to Test_Closet;

Object -> Test_Chamber_Table "table"
  has scenery supporter,
  with  name 'table' 'surface',
        description
          "~It's a stainless steel table with a smooth surface. Its legs
            are bolted to the floor.~",
        found_in Test_Chamber,
        before [;
          Taste:
            "~With all due respect, I'm not gonna lick the table. I don't
              know what kinda tests y'all been running in here. You can
              fire me if you want, but my tongue is not coming into 
              contact with this table.~";
          Drink:
            "~Drink... the table? Don't we have serious work to be doing?~";
          Touch:
            "~", (string) RandomAffirmative(),
            "touching the table. It's cool and smooth.~";
! TODO: add actual fingerprints object to table
        ];

Object -> Test_Personnel_1 "grumpy test subject"
  with name 'grumpy' 'personnel' 'subject',
       description "~One of the test subjects. He looks like he could eat me for breakfast and he's having a bad day. He's wearing a black jumpsuit, black combat boots, a black armor vest, and has a sidearm in a holster under his left arm.~",
       life [;
         Ask, Tell: "~He's just glaring at me. I'm not supposed to talk to him anyway.~";
         Attack: "~Even if that didn't directly contradict my orders, I don't think I could take this guy.~";
         Kiss: "~I'm certain that violates our employee conduct rules.~";
       ],
       before [;
         Take: "~I was hired for my small frame, quick reflexes, and ability to follow directions. This guy was apparently hired for his mass and poor attitude. There's no way.~";
       ],
  has animate transparent;

Clothes -> -> Jumpsuit "black jumpsuit"
  with name 'jumpsuit',
       description "~A black combat jumpsuit. It has various pockets to hold tactical gear.~",
  has clothing;

Clothes -> -> Combat_Boots "combat boots"
  with name 'combat' 'boots',
       description "~A pair of black combat boots. Large and intimidating.~",
  has clothing;

Object -> -> Pistol "sidearm"
  with name 'sidearm' 'pistol' 'weapon',
       description [;
         if( ( self in player or self.has_been_held ) == true )
         {
           "It's a semi-automatic pistol. Getting a closer look, I can tell it's a Beretta M9.";
         }
         else
         {
           "~It's a semi-automatic pistol. I can't tell from here what kind.~";
         };
       ],
       has_been_held false,
       before [;
         Take: 
           if( self in Test_Personnel_1 )
           {
             "~I'm not in the habit of breaking direct orders, or attempting to grab the gun of someone who's staring right at me.~";
           };
       ];


Object -> Test_Personell_2 "aloof test subject"
  with name 'aloof' 'personell' 'subject',
       description "~A test subject hired for these experiments. She's staring off into the distance, apparently trying to forget she's here.~",
  has clothing;

Room Test_Closet "Test Closet"
  has light,
  with name 'closet',
  description "~It's a small closet. There's two shelves, an upper and a lower.~",
  n_to Test_Chamber;


[ Initialise;
  location = Test_Chamber;
  move DisguiseAmulet to Test_Chamber_Table;
  move DestructionDisk to Test_Chamber_Table;
  move PortableHole to Test_Chamber_Table;
  move UniversalKey to Test_Chamber_Table;
            "^^^
              ~Is this thing on? It's certainly strange, thinking in
              text like this. Funny that they designed a neural comms
              implant that only communicates through text. Must be some
              reason the spooks want to keep it text-only. They don't
              pay me to wonder about the reasons they do things the way
              they do, though. They pay me to be disposable and test these
              devices.~^^";
];

Include "Grammar";

Verb 'lick' = 'taste';

[ BiteSub; "~Bite? Bite what?~"; ];
Verb 'nibble' 'chew' 'bite'
    * -> Bite
    * noun -> Bite
    * 'on' noun -> Bite;

Verb 'grind'
    * 'teeth' -> Bite;

[ SlideSub; "~Slide... what?~"; ];
Verb 'slide'
    * noun -> Slide;

[ RandomAffirmative;
    switch( random(3) )
    {
      1: return "Ok; ";
      2: return "Copy that; ";
      3: return "Alright; ";
    }
]; 

![ ExamineSub; "~I don't see what you're talking about...~" ]
